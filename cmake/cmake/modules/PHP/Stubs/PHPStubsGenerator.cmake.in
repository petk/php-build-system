#[=============================================================================[
Script for processing PHP stub sources.
#]=============================================================================]

set(sources "@PHP_SOURCES@")

# Ensure sources include only *.stub.php files.
list(FILTER sources INCLUDE REGEX [[\.stub\.php$]])

# Create a list of sources that must be parsed by the generator.
if(@PROJECT_SOURCE_DIR@/build/gen_stub.php IS_NEWER_THAN ${CMAKE_CURRENT_LIST_FILE})
  file(
    COPY
    @PROJECT_SOURCE_DIR@/build/gen_stub.php
    DESTINATION @PROJECT_BINARY_DIR@/CMakeFiles/php_stubs
  )
  set(stubs ${sources})
  file(TOUCH ${CMAKE_CURRENT_LIST_FILE})
else()
  foreach(stub ${sources})
    string(REGEX REPLACE [[\.stub\.php$]] [[_arginfo.h]] header "${stub}")
    if("${stub}" IS_NEWER_THAN "${header}")
      list(APPEND stubs ${stub})
    endif()
  endforeach()
endif()

if(NOT stubs)
  return()
endif()

execute_process(
  COMMAND
    ${CMAKE_COMMAND}
    -E cmake_echo_color --blue --bold
    "Regenerating *_arginfo.h headers from *.stub.php sources"
)

execute_process(
  COMMAND
    @PHP_COMMAND@ @PROJECT_BINARY_DIR@/CMakeFiles/php_stubs/gen_stub.php
    ${stubs}
)

# Ensure that *_arginfo.h headers are newer than their *.stub.php sources.
foreach(stub ${stubs})
  string(REGEX REPLACE [[\.stub\.php$]] [[_arginfo.h]] header "${stub}")
  if("${stub}" IS_NEWER_THAN "${header}")
    file(TOUCH "${header}")
  endif()
endforeach()

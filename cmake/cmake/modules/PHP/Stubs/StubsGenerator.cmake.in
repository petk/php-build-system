#[=============================================================================[
Script for processing PHP stub sources.

Expected variables:

* PHP_COMMAND
#]=============================================================================]

cmake_minimum_required(VERSION 4.2...4.3)

if(NOT CMAKE_SCRIPT_MODE_FILE)
  message(FATAL_ERROR "This is a command-line script.")
endif()

if(NOT PHP_COMMAND)
  message(WARNING "StubsGenerator.cmake: No PHP command given.")
  return()
endif()

set(sources "@PHP_SOURCES@")
set(php_gen_stub_script_source "@PROJECT_SOURCE_DIR@/build/gen_stub.php")

# Ensure sources include only *.stub.php files.
list(FILTER sources INCLUDE REGEX [[\.stub\.php$]])

# Create a list of sources that must be parsed by the generator.
if(${php_gen_stub_script_source} IS_NEWER_THAN ${CMAKE_CURRENT_LIST_FILE})
  file(COPY ${php_gen_stub_script_source} DESTINATION ${CMAKE_CURRENT_LIST_DIR})
  set(stubs ${sources})
  file(TOUCH ${CMAKE_CURRENT_LIST_FILE})
else()
  foreach(stub ${sources})
    string(REGEX REPLACE [[\.stub\.php$]] [[_arginfo.h]] arginfo_header "${stub}")
    string(REGEX REPLACE [[\.stub\.php$]] [[_decl.h]] decl_header "${stub}")
    if("${stub}" IS_NEWER_THAN "${arginfo_header}")
      list(APPEND stubs ${stub})
    elseif(EXISTS "${decl_header}" AND "${stub}" IS_NEWER_THAN "${decl_header}")
      list(APPEND stubs ${stub})
    endif()
  endforeach()
endif()

if(NOT stubs)
  return()
endif()

execute_process(
  COMMAND
    ${CMAKE_COMMAND} -E cmake_echo_color --blue --bold
      "Regenerating *_{arginfo,decl}.h headers from *.stub.php sources"
  COMMAND
    ${PHP_COMMAND} ${CMAKE_CURRENT_LIST_DIR}/gen_stub.php ${stubs}
)

# Ensure that *_{arginfo,decl}.h headers are newer than their *.stub.php sources.
foreach(stub ${stubs})
  string(REGEX REPLACE [[\.stub\.php$]] [[_arginfo.h]] arginfo_header "${stub}")
  string(REGEX REPLACE [[\.stub\.php$]] [[_decl.h]] decl_header "${stub}")
  if("${stub}" IS_NEWER_THAN "${arginfo_header}")
    file(TOUCH "${arginfo_header}")
  endif()
  if(EXISTS "${decl_header}" AND "${stub}" IS_NEWER_THAN "${decl_header}")
    file(TOUCH "${decl_header}")
  endif()
endforeach()

#[=============================================================================[
# PHP %EXTNAME% extension

Configure the PHP `%EXTNAME%` extension.

## Configuration options

### PHP_EXT_%EXTNAMECAPS%

* Default: `OFF`
* Values: `ON|OFF`

Enable the extension.

### PHP_EXT_%EXTNAMECAPS%_SHARED

* Default: `OFF`
* Values: `ON|OFF`

Build extension as shared.
#]=============================================================================]

# CMakeLists.txt file for the %EXTNAME% extension.

# Set minimum required CMake version.
cmake_minimum_required(VERSION 4.2...4.3)

# Define the PHP extension project and its metadata.
project(
  PHP_EXT_%EXTNAMECAPS%
  VERSION 0.1.0
  DESCRIPTION "Describe the extension"
  HOMEPAGE_URL "https://example.org"
  LANGUAGES C
)

# Include CMake module to use cmake_dependent_option().
include(CMakeDependentOption)

# Include CMake module to use add_feature_info() and set_package_properties().
include(FeatureSummary)

# Boolean option that enables (ON) or disables (OFF) the extension. For example,
# cmake -B <build-dir> -D PHP_EXT_%EXTNAMECAPS%=ON
option(
  PHP_EXT_%EXTNAMECAPS%
  "Enable the %EXTNAME% extension"
  ${PROJECT_IS_TOP_LEVEL}
)

# Extension features are visible in the configuration summary output.
add_feature_info(
  "ext/%EXTNAME%"
  PHP_EXT_%EXTNAMECAPS%
  "short description of extension features"
)

# Dependent boolean option that builds extension as a shared library.
cmake_dependent_option(
  PHP_EXT_%EXTNAMECAPS%_SHARED
  "Build the %EXTNAME% extension as a shared library"
  ${PROJECT_IS_TOP_LEVEL}
  PHP_EXT_%EXTNAMECAPS%
  OFF
)

# If extension is disabled CMake configuration stops here.
if(NOT PHP_EXT_%EXTNAMECAPS%)
  return()
endif()

set(CMAKE_C_STANDARD 99)
set(CMAKE_C_STANDARD_REQUIRED TRUE)

set(CMAKE_C_VISIBILITY_PRESET "hidden")

# Find PHP package on the host.
find_package(PHP REQUIRED)

# Configure PHP extension.
include(PHP/Extension)
php_extension(%EXTNAME%)

# Add a target to be built as a dynamically loadable module (shared extension),
# or statically.
if(PHP_EXT_%EXTNAMECAPS%_SHARED)
  add_library(php_ext_%EXTNAME% MODULE)
else()
  add_library(php_ext_%EXTNAME% OBJECT)
endif()

# Configure extension as CLI-based. Such extensions are only meant to be used
# and enabled on CLI-based PHP SAPIs, such as cgi, cli, phpdbg, embed, etc.
#set_target_properties(php_ext_%EXTNAME% PROPERTIES PHP_CLI TRUE)

# Configure extension as Zend extension. Zend extensions are loaded with the
# 'zend_extension' INI directive and include additional advanced hooks. Use only
# when building advanced extensions, such as debuggers, profilers, caching, etc.
#set_target_properties(php_ext_%EXTNAME% PROPERTIES PHP_ZEND_EXTENSION TRUE)

# Add library target sources.
target_sources(
  php_ext_%EXTNAME%
  PRIVATE
    %EXTNAME%.c
    %EXTNAME%.stub.php
  # Add headers to a file set, if extension provides headers that should be
  # installed and will be consumed by other sources.
  PUBLIC
    FILE_SET HEADERS
      FILES
        php_%EXTNAME%.h
)

# Add compile definitions.
target_compile_definitions(
  php_ext_%EXTNAME%
  PRIVATE
    # Enable "Thread Safety Resource Manager Local Storage" static cache.
    ZEND_ENABLE_STATIC_TSRMLS_CACHE
)

include(PHP/SystemExtensions)
target_link_libraries(php_ext_%EXTNAME% PRIVATE PHP::SystemExtensions)

target_compile_options(
  php_ext_%EXTNAME%
  PRIVATE
    -g
    -fdiagnostics-color=always
    -fno-common
    -Wstrict-prototypes
    -Wformat-truncation
    -Wlogical-op
    -Wduplicated-cond
    -Wno-clobbered
    -Wall
    -Wextra
    -Wno-unused-parameter
    -Wno-sign-compare
    -Wimplicit-fallthrough=1
)

target_include_directories(
  php_ext_%EXTNAME%
  PRIVATE ${CMAKE_CURRENT_BINARY_DIR}
)

set(PHP_EXT_%EXTNAMECAPS%_ENABLED TRUE)

# Create configuration header.
configure_file(cmake/config.h.in config.h)
